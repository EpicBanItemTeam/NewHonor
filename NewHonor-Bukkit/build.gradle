import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
    }
}

plugins {
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'net.ltgt.apt' version '0.10'
}

blossom {
    //参考build.gradle https://github.com/ginyai/GiftCode/blob/master/build.gradle
    replaceToken('@bukkitVersion@', bukkitVersion)
}

apply plugin: 'java'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

defaultTasks 'clean', 'build'

jar {
    manifest {
        attributes 'Main-Class': 'com.github.euonmyoji.newhonor.Main'
    }
}

def buildNumber = ""
ext {
    //代码来自插件ebi中的build.gradle → : https://github.com/euOnmyoji/EpicBanItem---Sponge
    //noinspection GrDeprecatedAPIUsage  不会用新版
    git = Grgit.open(file("../.git"))
    parents = git.head().parentIds
    index = 0// Offset to match CI
    while (parents != null && !parents.isEmpty()) {
        parents = git.getResolve().toCommit(parents.get(0)).getParentIds()
        index++
    }
    //noinspection GrReassignedInClosureLocalVar
    buildNumber = "${index}"
}

repositories {
    mavenCentral()
    //bukkit
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}
group = 'com.github.euonmyoji.newhonor'
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

configurations {
    //http://www.mcbbs.net/forum.php?mod=redirect&goto=findpost&ptid=825934&pid=13448567
    //哇 这个操作 真的舒服awa
    shade
    compile.extendsFrom(shade)
}

dependencies {
    shade 'ninja.leaping.configurate:configurate-core:3.3'
    shade 'ninja.leaping.configurate:configurate-hocon:3.3'
    shade 'com.google.guava:guava:21.0'

    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.4'
    apt 'org.projectlombok:lombok:1.18.4'

    //bukkit
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'

    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':NewHonor-Common')
}
version = "${bukkitVersion}-b${buildNumber}"

shadowJar() {
    manifest {
        attributes 'Main-Class': 'com.github.euonmyoji.newhonor.Main'
    }
    configurations = [project.configurations.compile]
    classifier = ""
    from sourceSets.main.output
}

sourceSets {
    main {
        java {
            srcDir '../NewHonor-Common/src/main/java'
        }
        resources {
            srcDir '../NewHonor-Common/src/main/resources'
        }
    }
}

artifacts {
    archives shadowJar
}

jar.setEnabled(true)

buildDir = '../build/bukkit'