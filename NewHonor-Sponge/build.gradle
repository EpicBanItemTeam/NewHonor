import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
    }
}

plugins {
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

defaultTasks 'clean', 'build'

jar {
    manifest {
        attributes 'Main-Class': 'com.github.euonmyoji.newhonor.Main'
    }
}
group = 'com.github.euonmyoji.newhonor'
def buildNumber = ""
ext {
    //代码来自插件ebi中的build.gradle → : https://github.com/euOnmyoji/EpicBanItem---Sponge
    //noinspection GrDeprecatedAPIUsage  不会用新版
    git = Grgit.open(file("../.git"))
    parents = git.head().parentIds
    index = 0// Offset to match CI
    while (parents != null && !parents.isEmpty()) {
        parents = git.getResolve().toCommit(parents.get(0)).getParentIds()
        index++
    }
    //noinspection GrReassignedInClosureLocalVar
    buildNumber = "${index}"
}

blossom {
    //参考build.gradle https://github.com/ginyai/GiftCode/blob/master/build.gradle
    replaceToken('@spongeVersion@', spongeVersion)
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = "nucleus"
        url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft"
    }
    maven { url = 'https://raw.github.com/FabioZumbi12/UltimateChat/mvn-repo/' }
    maven { url = 'https://jitpack.io' }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeapi}"
    compileOnly 'br.net.fabiozumbi12.UltimateChat:UltimateChat-Sponge-5.0-6.0:1.8.6'
    compileOnly 'com.github.rojo8399:PlaceholderAPI:master-SNAPSHOT'
    compileOnly 'io.github.nucleuspowered:nucleus-api:1.3.9-S7.0'
    compile 'org.bstats.bStats-Metrics:bstats-sponge:1.3'

    compile project(":NewHonor-Common")
}
version = "${spongeVersion}-b${buildNumber}"
//以下有关shadow代码由百度搜索引擎提供技术支持(雾)
shadowJar() {
    from sourceSets.main.output

    classifier = ""
    dependencies {
        include((Spec<FileTreeElement>) dependency("org.bstats.bStats-Metrics:bstats-sponge:1.3"))
    }
}

task spongeApiJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.github.euonmyoji.newhonor.Main'
    }
    version = "${spongeVersion}"
    classifier = "API"
    from sourceSets.main.output
    include 'com/github/euonmyoji/newhonor/api/**', 'com/github/euonmyoji/newhonor/api/**'
}

sourceSets {
    main {
        java {
            srcDir '../NewHonor-Common/src/main/java'
        }
        resources {
            srcDir '../NewHonor-Common/src/main/resources'
        }
    }
}

artifacts {
    archives shadowJar
    archives spongeApiJar
}

jar.setEnabled(true)

buildDir = '../build/sponge'
